#!/usr/bin/env groovy

/**
 * This Jenkinsfile builds the dcos-cli into
 * a binary on windows and executes it.
 */

/**
 * The `builder` function returns a closure that runs
 * a build on a specific node in a specific workspace.
 *
 * If no workspace is specified, the current workspace is used.
 */
def builder(String nodeId, String workspace = null) {
    return { Closure _body ->
        def body = _body

        return {
            node(nodeId) {
                if (!workspace) {
                    workspace = "${env.WORKSPACE}"
                }

                ws (workspace) {
                    stage ('Cleanup workspace') {
                        deleteDir()
                    }

                    body()
                }
            }
        }
    }
}


/**
 * Run a builder to create the dcos-cli binary and execute it.
 */
def binaryBuilder = builder('windows')({
    stage ('Pull dcos-cli repository') {
        dir('dcos-cli') {
            checkout([
                $class: 'GitSCM',
                userRemoteConfigs: scm.userRemoteConfigs,
                branches: scm.branches,
                doGenerateSubmoduleConfigurations: scm.doGenerateSubmoduleConfigurations,
                submoduleCfg: scm.submoduleCfg,
                extensions: [
                    [
                        $class: 'CloneOption',
                        shallow: true,
                        depth: 0,
                        noTags: true,
                        timeout: 10
                    ]
                ]
            ])
        }
    }

    stage ("Build dcos-cli binary") {
        dir('dcos-cli/cli') {
            bat 'bash -c "make binary"'
            bat 'dist\\dcos.exe'
        }
    }
})

binaryBuilder()
